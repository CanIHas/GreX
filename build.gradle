plugins {
    id 'groovy'
    id 'idea'
    id 'maven'
    id 'maven-publish'
    id 'com.jfrog.bintray' version '1.0'
}

group="can.i.has"
sourceCompatibility = 1.7
targetCompatibility = 1.7
version = '0.1'

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    // Common dependencies
    compile localGroovy()
    compile 'org.slf4j:slf4j-api:1.7.2'
    compile 'de.ruedigermoeller:fst:2.20'

    testCompile 'junit:junit:4.11'

    runtime 'org.slf4j:slf4j-log4j12:1.7.2'

}

test {
    useJUnit()
}

task javadocJar(type: Jar, dependsOn: groovydoc) {
    from "${buildDir}/docs/groovydoc"
    classifier = 'javadoc'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            pom.withXml {
                asNode().children().last() + {
                    resolveStrategy = DELEGATE_FIRST
                    name project.name
                    description project.description
                    url "https://github.com/CanIHas/GreX"
                    scm {
                        url "https://github.com/CanIHas/GreX"
                        connection "scm:git:https://github.com/CanIHas/GreX"
                    }
                    licenses {
                        license {
                            name "The MIT License"
                            distribution 'repo'
                        }
                    }
                }
            }
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER') ?: project.properties.bintray_username
    key  = System.getenv('BINTRAY_KEY')  ?: project.properties.bintray_api_key
    publications = ['maven']
    pkg {
        repo = 'maven'
        name = project.name
        version project.version
    }
}

tasks.bintrayUpload.doFirst {
    assert !project.version.contains('SNAPSHOT')
}

task wrapper(type: Wrapper) {
    gradleVersion = '2.3'
}